description = "Embulk core's hidden dependencies."
ext {
    summary = "Embulk core's hidden dependencies."
}

repositories {
    mavenCentral()
}

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()
}

dependencies {
    compileOnly project(":embulk-api")
    compileOnly project(":embulk-spi")
    compileOnly project(":embulk-core")

    // Buffer
    api "io.netty:netty-buffer:4.0.44.Final"
    api "io.airlift:slice:0.9"

    // CLI
    api "commons-cli:commons-cli:1.3.1"
    api "org.apache.velocity:velocity:1.7"

    // Config
    api "org.yaml:snakeyaml:1.18"

    // Guess
    api "com.ibm.icu:icu4j:54.1.1"

    // Maven
    api "org.apache.maven:maven-artifact:3.6.1"
    api "org.apache.maven.resolver:maven-resolver-api:1.3.3"
    api "org.apache.maven.resolver:maven-resolver-spi:1.3.3"
    api "org.apache.maven.resolver:maven-resolver-util:1.3.3"
    api("org.apache.maven.resolver:maven-resolver-impl:1.3.3") {
        exclude group: "org.slf4j", module: "slf4j-api"  // Included in embulk-core.
    }
    api("org.apache.maven:maven-resolver-provider:3.6.1") {
        exclude group: "javax.inject", module: "javax.inject"  // Included in embulk-core.
        exclude group: "com.google.inject", module: "guice"  // Included in embulk-core.
        exclude group: "org.slf4j", module: "slf4j-api"  // Included in embulk-core.
    }

    // Timestamp
    api "org.embulk:embulk-util-timestamp:0.2.1"

    testImplementation project(":embulk-api")
    testImplementation project(":embulk-spi")
    testImplementation project(":embulk-core")
    testImplementation "junit:junit:4.12"
}

task prepareDependencyJars(type: Copy, dependsOn: "jar") {
    doFirst {
        delete file("${buildDir}/dependency_jars")
        mkdir file("${buildDir}/dependency_jars")
    }
    into "${buildDir}/dependency_jars"
    from configurations.runtimeClasspath
    from jar.outputs.files
}
